# 实验一    环境熟悉与I/O地址译码

## 一、实验目的

​	掌握I/O地址译码电路的工作原理。

## 二、实验内容

1. 连接AD4\~AD9等几根线，使得138译码器Y0#\~Y7#译码出的地址依次分别是Y0#：200H\~20FH; Y1#：210H\~21FH；……；Y7#：270h\~27FH
2. 利用Source->Add/Remove Source files中添加源程序EX-138.ASM（在实验包中已提供），该程序向240H端口写任意数据。如果是第一次运行，Source Code Filename这一项选New来调入源程序。
3. 在Source菜单下点击“Build All”，汇编源代码（全部编译）；执行程序，查看Y0~Y7中是否Y4#脚输出为有效（管脚出的有颜色的小方块为红色是高电平，蓝色为低电平）.
4. 改变程序中的输出地址，使得Y0#, Y1#,Y6#,Y7#分别有效。
5. 改变A9\~A3的接线方法，从而得到Y0；388H\~38FH；Y1：398H～39FH; ……；Y7：3F8H\~3FFH。并修改上一问的程序，以同样使得Y4#有效。

### 1）源程序

```assembly
.8086
.MODEL SMALL
.STACK
.DATA
	ADDRESS  WORD 3C8H; 此值会变
.CODE
START:
    MOV AX,@DATA
    MOV DS,AX
    MOV DX,ADDRESS
    MOV AL,0
    OUT DX,AL
    JMP $
END START
```

### 2）电路原理图（138译码部分）

#### 1.

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.0.1.png)

#### 2.

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.0.2.png)

### 3）运行结果贴图（138译码及上面两个273的输出）

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.1.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.2.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.3.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.4.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.5.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/1.6.png)

# 实验二    可编程中断控制器8259

## 一、实验目的

1． 掌握8259的基本工作原理和编程方法。

2． 深入了解中断的概念和实地址模式下中断处理程序的编写方法。

## 二、实验内容

​	将接口实验包中所带的EX-8259.DSN文件用ISIS 7打开。按手册接线并执行。

```assembly
.MODEL SMALL
.8086
.stack
.data
come byte 00000000B   ; 计算中断到来次数的变量
.code
start:
    mov ax,@data
    mov ds,ax
    cli
    mov ax,0        ; init interrupt vector TABLE
    mov es,ax
    mov si,0         ; 由于模拟器的错误，所有中断向量需要指向同一个地址
    mov cx,255
l:  mov ax,offset int0
    mov es:[si],ax
    mov ax,seg int0
    mov es:[si+2],ax
    add si,4
    loop l
    mov ax,offset int0
    mov es:[si],ax
    mov ax,seg int0
    mov es:[si+2],ax
    mov ax,@data
    mov ds,ax
    mov al,00010011b  ;  init  8259
    mov dx,210h
    out dx,al         ; ICW1
    mov al,60h
    mov dx,212h
    out dx,al         ; ICW2
    mov ax,00000001h
    out dx,al         ; ICW4
    mov ax,0
    out dx,al         ; OCW1
    sti
    jmp $
int0  proc
    push ax
    push dx
    push ds
    mov ax,@data
    mov ds,ax   
    mov ax,0
    mov al, come     ; 中断到来，计数器+1
    inc al
    mov come, al
    mov al,20h       ; 发EOI命令
    mov dx,210h
    out dx,al
    pop ds
    pop dx
    pop ax
    iret
int0 endp
END start

```



## 三、运行结果贴图（执行三次中断，每次中断后的8086寄存器的截图）

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/2.1.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/2.2.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/2.3.png)

# 实验三    可编程定时器计数器8253

## 一、实验目的

​	掌握8253的基本工作原理、编程方法及其应用。

## 二、实验内容

### 一） 研究定时计数器（选）

#### 1）源程序

```assembly
.8086
.MODEL SMALL
.STACK
.DATA
.CODE
START:
    MOV AL,00010001B; 使用计数器0，只写低八位，模式0，BCD码
    MOV DX,226H
    OUT DX,AL
    MOV AL,10H
    MOV DX,220H
    OUT DX,AL
END START
```

#### 2）讨论题

​	如果把方式0改成方式1，电路不动，则按下BUTTON后，计数器值会否减1？为什么？

- 不会，因为模式一计数是从gate处于上升沿时开始，而这里gate连接电源，始终保持高电平，不会出现上升沿。

#### 3）接线原理图和运行结果图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/3.1.png)

### 二）信号发生器

#### 1）源程序

```assembly
.8086
.MODEL SMALL
.STACK
.DATA
.CODE
START:
    MOV AL,00110111B; 使用计数器0，先写低八位，后写高八位，使用模式三，BCD码
    MOV DX,226H
    OUT DX,AL
    MOV AX,1000H
    MOV DX,220H
    OUT DX,AL
    MOV AL,AH
    OUT DX,AL; 计数器初始值为1000
    
    MOV AL,01110111B; 使用计数器1，先写低八位，后写高八位，使用模式三，BCD码
    MOV DX,226H
    OUT DX,AL
    MOV AX,1000H
    MOV DX,222H
    OUT DX,AL
    MOV AL,AH
    OUT DX,AL; 计数器初始值为1000
    
    MOV AL,10010111B; 使用计数器2，只使用低八位，使用模式三，BCD码
    MOV DX,226H
    OUT DX,AL
    MOV AL,5H
    MOV DX,224H
    OUT DX,AL; 计数器初始值为5
END START
```

#### 2）接线原理图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/3.3.png)

#### 3）运行结果图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/3.2.png)

#### 4）编程与调试心得（遇到的问题和解决的办法，以及获得的收获）

​	对于OUT2口的波形如果不取反的话就会得到占空比为3:5的方波。

# 实验五    七段数码管实验

## 一、实验目的

​	掌握数码管显示数字的原理，进一步复习8255的应用。

## 二、实验内容

### 1.静态显示（选）

#### 1）源程序

```assembly
.8086
.MODEL SMALL
.STACK
.DATA
	ARR BYTE 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH,77H,7CH,39H,5EH,79H,71H
.CODE
START:
	MOV AX,@DATA
	MOV DS,AX
	MOV AL,10000001B; A组模式0，A口和C口高四位为输出。B组模式0，B口为输出，C口低四位为输入。
	MOV DX,206H
	OUT DX,AL
	MOV DX,204H
	IN AL,DX
	NOT AL
	MOV AH,0H
	MOV DL,16
	DIV DL
	MOV AL,AH
	MOV AH,0H
	MOV BX,AX
	LEA DI,ARR
	MOV AL,[DI+BX]
	MOV DX,200H
	OUT DX,AL
	JMP $
END START
```

#### 2）接线原理图和运行结果图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/5.1.png)

### 2.动态展示

#### 1）源程序

```assembly
.8086
.MODEL SMALL
.STACK
.DATA
	ARR BYTE 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH,77H,7CH,39H,5EH,79H,71H
.CODE
START:
	MOV AX,@DATA
	MOV DS,AX
	MOV AL,10001001B
	MOV DX,206H
	OUT DX,AL
	LEA DI,ARR
LP:
	MOV DX,204H
	IN AL,DX
	NOT AL
	MOV AH,0
	MOV DL,16
	DIV DL
	MOV SI,AX
	MOV AL,01H
	MOV DX,202H
	OUT DX,AL
	MOV BX,SI
	MOV BH,0
	MOV DX,200H
	MOV AL,[DI+BX]
	OUT DX,AL
	MOV CX,64H
	LOOP $
	MOV AL,02H
	MOV DX,202H
	OUT DX,AL
	MOV BX,SI
	MOV BL,BH
	MOV BH,0
	MOV DX,200H
	MOV AL,[DI+BX]
	OUT DX,AL
	MOV CX,64H
	LOOP $
	JMP LP
END START
```

#### 2）接线原理图和运行结果图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/5.2.png)

#### 3）编程与调试心得（遇到的问题和解决的办法，以及获得的收获）

​	刚开始只有一个数字有显示，后来才想到通过PB0/1片选高位还是低位亮。其实是高位和低位交替亮。这个实验让我对8255的使用有了更深的体会。

# 实验六    交通灯控制实验

## 一、实验目的

​	通过并行接口8255实现十字路口交通灯的模拟控制,进一步掌握对并行口的使用和中断的使用。

## 二、交通灯变化规律

十字路口交通灯的变化规律要求

1） 南北路口的绿灯、东西路口的红灯同时亮10秒，然后

2） 南北路口的黄灯闪烁2次（闪烁周期2秒），同时东西路口的红灯继续亮，然后

3） 南北路口的红灯、东西路口的绿灯同时亮10秒，然后

4） 南北路口的红灯继续亮，同时东西路口的黄灯2次（闪烁周期2秒），然后

5） 转1重复。

## 三、实验内容

​	红黄绿三色交通灯控制。将接口实验包中所带的EX-825X-1.DSN文件用ISIS 7打开，将D6、D5、D4作为南北路口的交通灯与PA7、PA6、PA5相连；D3、D2、D1作为东西路口的交通灯与PA2、PA1、PA0相连(方式0)。利用8253产生1秒的中断信号(系统已经为CLK0提供了一个1MHz的时钟输入)，在中断处理程序中用程序处理10秒延迟和两次黄灯闪烁的问题。编程使六个灯按交通灯变化规律燃灭。

### 1）源程序

```assembly
 .8086
.MODEL SMALL
.STACK
.DATA
	COUNT BYTE 0H
.CODE
START:
	MOV AX,@DATA
	MOV DS,AX
	MOV DX,226H
	MOV AL,00110111B
	OUT DX,AL
	MOV AX,1000H
	MOV DX,220H
	OUT DX,AL
	MOV AL,AH
	OUT DX,AL  ;C0
	MOV DX,226H
	MOV AL,01110111B
	OUT DX,AL
	MOV AX,1000H
	MOV DX,222H
	OUT DX,AL
	MOV AL,AH
	OUT DX,AL  ;C1
	MOV AL,10000000B
	MOV DX,206H
	OUT DX,AL
	MOV AL,10000001B
	MOV DX,200H
	OUT DX,AL
	CLI
	MOV AX,0
	MOV ES,AX
	MOV SI,0
	MOV CX,255
L:  MOV AX,OFFSET INT0
	MOV ES:[SI],AX
	MOV AX, SEG INT0
	MOV ES:[SI+2],AX
	ADD SI,4
	LOOP L
	MOV AX,OFFSET INT0
	MOV ES:[SI],AX
	MOV AX,SEG INT0
	MOV ES:[SI+2],AX
	MOV AX,@DATA
	MOV DS,AX 
	STI
	MOV AL,00010011B  ;  INIT  8259
	MOV DX,210H
	OUT DX,AL         ; ICW1
	MOV AL,00001000B
	MOV DX,212H
	OUT DX,AL
	MOV AL,01H
	OUT DX,AL
	JMP $
	INT0 PROC
	PUSH SI
	PUSH AX
	PUSH DX
	PUSH DS
	MOV AX,@DATA
	MOV DS,AX
	MOV AL,COUNT
	INC AL
	MOV COUNT,AL
	CMP AL,10
	JZ DO1
	CMP AL,11
	JZ DO2
	CMP AL,12
	JZ DO1
	CMP AL,13
	JZ DO2
	CMP AL,14
	JZ DO3
	CMP AL,24
	JZ DO4
	CMP AL,25
	JZ DO5
	CMP AL,26
	JZ DO4
	CMP AL,27
	JZ DO5
	CMP AL,28
	JZ DO6
	JMP EXIT
DO6:
	MOV AL,10000001B ;左绿右红
	MOV DX,200H
	OUT DX,AL
	MOV AL,0
	MOV COUNT,AL
	JMP EXIT
DO1:
	MOV AL,01000001B ;左黄灯亮
	MOV DX,200H
	OUT DX,AL
	JMP EXIT
DO2:
	MOV AL,00000001B ;左黄灯暗
	MOV DX,200H
	OUT DX,AL
	JMP EXIT
DO3:
	MOV AL,00100100B ;左红右绿
	MOV DX,200H
	OUT DX,AL
	JMP EXIT
DO4:
	MOV AL,00100010B;右黄灯亮
	MOV DX,200H
	OUT DX,AL
	JMP EXIT
DO5:
	MOV AL,00100000B ;右黄灯暗
	MOV DX,200H
	OUT DX,AL
	JMP EXIT
EXIT:
	MOV AL,20H
	MOV DX,210H
	OUT DX,AL
	POP DS
	POP DX
	POP AX
	POP SI
	IRET
	INT0 ENDP
END START
```

### 2）接线原理图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/6.1.png)

### 3）运行结果截图

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/6.2.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/6.4.png)

![](https://raw.githubusercontent.com/ZizhuoWang/ImageBed/master/IO/6.3.png)

### 4）编程与调试心得（遇到的问题和解决的办法，以及获得的收获）

​	循环很多，一不小心就会写错。如何让黄灯闪烁也想了很久。